apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "org.strongswan.android"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 62
        versionName "2.0.2"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    flavorDimensions "default"

    productFlavors {
        strongswan {
            applicationId "org.strongswan.android"
            versionName "1.6.2"
        }
        fancyfon {
            applicationId "com.fancyfon.strongswan"
            versionName "1.3.0"
            versionCode 34
            setProperty("archivesBaseName", "strongswan_$versionName")

        }
    }

    sourceSets.main {
        jni.srcDirs = [] // disables the default ndk-build call (with on-the-fly Android.mk files)
        jniLibs.srcDir 'src/main/libs'
    }

       task buildNative(type: Exec) {
           workingDir 'src/main/jni'
           commandLine "${android.ndkDirectory}/ndk-build", '-j', Runtime.runtime.availableProcessors()

       }
       task cleanNative(type: Exec) {
           workingDir 'src/main/jni'
           commandLine "${android.ndkDirectory}/ndk-build", 'clean'
       }

    tasks.withType(JavaCompile) {
        compileTask ->
            if(!project.hasProperty("native-disabled")) {
                compileTask.dependsOn buildNative
            }
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

    }

    if(!project.hasProperty("native-disabled")) {
        clean.dependsOn 'cleanNative'
    }

    buildTypes {
        release {
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}



dependencies {
    implementation 'com.android.support:appcompat-v7:26.0.2'
    implementation 'com.android.support:design:26.0.2'
    implementation 'com.android.support:preference-v7:26.0.2'
    implementation 'com.android.support:support-v4:26.0.2'
    testImplementation 'junit:junit:4.12'

    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.greenrobot:eventbus:3.0.0'

    //fancyfon libs
    compile project (':api')
    compile group: 'com.fancyfon.mobile.android.verification', name: 'Android_Caller_Verification_Library', version: '1.0.2'
}